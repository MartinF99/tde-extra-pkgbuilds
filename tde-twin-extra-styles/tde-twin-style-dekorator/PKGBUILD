# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=twin-style-dekorator
_cat=applications/themes/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.0.12
pkgrel=1
pkgdesc="Trinity Desktop Enviroment $_cat$_mod"
arch=('i686' 'x86_64')
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
provides=("${_mod}")
groups=('tde-complete')
depends=(
  'tde-tdebase'
)
makedepends=(
  'tde-cmake-trinity'
  'autoconf'
  'pkgconfig'
)
optdepends=()
conflicts=("trinity-${_kdemod}" "kdemod3-${_kdemod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!strip')
install=

source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
sha512sums=('9d91edc82deb5b654a0420b3ea02b7bf750e6698264123accba71825661d57a015c7c5afb0215654559ae9f11d47ac996cd61fb7ca815072d27a977c96f18bf8')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$QTDIR" ] || QTDIR=${TDEDIR}/tqt3

build() {
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  msg "Starting cmake..."
  cmake ${srcdir}/${_mod}-trinity-${pkgver} \
    -GNinja \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DSYSCONF_INSTALL_DIR=${TDEDIR}/etc \
    -DBUILD_ALL=ON \
    -DWITH_ALL_OPTIONS=ON

  msg "Building - ${pkgname}..."
  ninja $NUMJOBS
}

package() {
  msg "Packaging - ${pkgname}-${pkgver}"
  cd "$srcdir/build"
  DESTDIR="$pkgdir" ninja -j1 install
}
